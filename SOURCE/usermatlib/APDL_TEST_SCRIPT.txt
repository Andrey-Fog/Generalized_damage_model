FINISH
/clear
STEPS=100
DDY	=0.005
/PREP7
ET,1,SOLID185    

!*************************** MECHANICAL PROPERTIES 
*SET,YOUNG ,210000  !-Young's molulus   
*SET,XNUE ,0.3  !-Poisson's ratio   
*SET,YELDSTR ,744         !-initial yielding strength (MPa) 
*SET,SHEXP ,0.1         !-power value for nonlinear isotropic hardening power law   
*SET,NORTB ,6E-10 !-Norton  law B const 
*SET,NORTN ,2.54  !-power value for Norton  law 
!***ELASTIC 
MPTEMP,,,,,,,,  
MPTEMP,1,0  
MPDATA,EX,1,,YOUNG  
MPDATA,PRXY,1,,XNUE 
!***ISOTROPIC HARDERNING
TB,NLISO,1,,2,POWER 
TBDATA,1,YELDSTR,SHEXP  
!***CREEP   
TB,CREE,1,1,3,10
TBTEMP,0
TBDATA,,NORTB,NORTN,0,,,
!*************************** GEOMETRY
BLC4,0,0,1,1,1  

!*************************** MESHING
LESIZE,ALL, , ,1, , , , ,0 
VMESH,1 

!*************************** LOADS
DA,5,UX, 
DA,3,UY, 
DA,1,UZ, 

DA,4,UY,DDY

!*************************** SOLUTION
FINISH 
/SOL
ANTYPE,0
TIME,1
AUTOTS,0
NSUBST,STEPS,STEPS,STEPS
OUTRES,ALL,ALL
SOLVE


!*************************** RESULTS
FINISH 
/POST1
*DIM, DSPL, TABLE, STEPS,2,,,,, 
*do, I, 1,STEPS,1
	SET,,, ,,, ,I
	*GET,Par1, NODE, 1, S, EQV
	DSPL(I,1,1)=Par1
	*GET,Par2, NODE, 1, EPTO, EQV
	DSPL(I,2,1)=Par2
*ENDDO
DSPL(1,1,1)=0
DSPL(1,2,1)=0
PLESOL, S,EQV, 0,1.0 
*VPLOT,DSPL(1,2,1),DSPL(1,1,1), , , , , , , ,
/REPLOT
