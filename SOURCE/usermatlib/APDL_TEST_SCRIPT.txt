FINISH
/clear
STEPS=100
DDY	=0.003
/PREP7
ET,1,PLANE182    
KEYOPT,1,1,1
KEYOPT,1,3,1
KEYOPT,1,6,1

!*************************** MECHANICAL PROPERTIES 
YOUNG	=215000		!-Young's molulus
XNUE	=0.3		!-Poisson's ratio
YELDSTR	=265        	!-initial yielding strength (MPa)
SHEXP	=0.1217        	!-power value for nonlinear isotropic hardening power law

!***ELASTIC
MPTEMP,,,,,,,,  
MPTEMP,1,0  
MPDATA,EX,1,,YOUNG 
MPDATA,PRXY,1,,XNUE

!***ISOTROPIC HARDERNING
TB,NLISO,1,,2,POWER
TBDATA,1,YELDSTR,SHEXP

!*************************** GEOMETRY
BLC4, 0,0,0.002,0.005

!*************************** MESHING
SMRT,6  
!MSHAPE,0,2D 
!MSHKEY,0
AMESH,ALL  

!*************************** LOADS
DL,       4, ,SYMM 
DL,1, ,UY,   
DL,3, ,UY,DDY

!*************************** SOLUTION
FINISH 
/SOL
ANTYPE,0
TIME,1
AUTOTS,0
NSUBST,STEPS,STEPS,STEPS
OUTRES,ALL,ALL
SOLVE


!*************************** RESULTS
FINISH 
/POST1
*DIM, DSPL, TABLE, STEPS,2,,,,, 
*do, I, 1,STEPS,1
	SET,,, ,,, ,I
	*GET,Par1, NODE, 1, S, EQV
	DSPL(I,1,1)=Par1
	*GET,Par2, NODE, 1, EPTO, EQV
	DSPL(I,2,1)=Par2
*ENDDO
DSPL(1,1,1)=0
DSPL(1,2,1)=0
PLESOL, S,EQV, 0,1.0 
*VPLOT,DSPL(1,2,1),DSPL(1,1,1), , , , , , , ,
/REPLOT
